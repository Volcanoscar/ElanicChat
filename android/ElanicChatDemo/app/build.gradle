apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1

        buildConfigField"String", "LOCAL_IP", '\"' + getLocalIp('en0') + '\"'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {

        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        ws {
            applicationId "in.elanic.elanicchatdemo"
            versionName "elanic_chat_ws"
            buildConfigField"String", "LOCAL_PORT", "\"9999\""
        }

        sio {
            applicationId "in.elanic.elanicchatdemo_sio"
            versionName "elanic_chat_sio"
            buildConfigField"String", "LOCAL_PORT", "\"9900\""
        }

    }
}

def getLocalIp(String interfaceName) {
    NetworkInterface iface = NetworkInterface.getByName(interfaceName);
    for(InterfaceAddress address : iface.getInterfaceAddresses()) {
        String ip = address.getAddress().getHostAddress();
        if (ip.length() <= 15) {
            return ip;
        }
    }
}

final DAGGER_VERSION = '2.0.1'
final DEXMAKER_VERSION = '1.2'
final RUNNER_VERSION = '0.4'
final ESPRESSO_VERSION = '2.2.1'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'

    // Dagger
    compile "com.google.dagger:dagger:${DAGGER_VERSION}"
    provided 'org.glassfish:javax.annotation:10.0-b28'
    apt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    // Database
    compile 'de.greenrobot:greendao:2.0.0'

    //Butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    // RxJava
    compile 'io.reactivex:rxjava:1.0.0'
    compile 'io.reactivex:rxandroid:0.24.0'

    // EventBus
    compile 'de.greenrobot:eventbus:2.4.0'

    // Websocket
    compile 'com.neovisionaries:nv-websocket-client:1.17'

    // Socket.io
    compile ('com.github.nkzawa:socket.io-client:0.3.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }

    // Retrofit
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'

    // Material dialogs
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }

    // Test
    testCompile "com.google.dagger:dagger:${DAGGER_VERSION}"
    testCompile 'junit:junit:4.12'
    testProvided "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    testCompile 'com.android.support:support-annotations:23.1.1'
    testCompile("com.android.support.test:runner:${RUNNER_VERSION}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    testCompile 'org.mockito:mockito-core:1.10.19'

    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile("com.android.support.test:runner:${RUNNER_VERSION}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile "com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${ESPRESSO_VERSION}"
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile "com.google.dexmaker:dexmaker:${DEXMAKER_VERSION}"
    androidTestCompile "com.google.dexmaker:dexmaker-mockito:${DEXMAKER_VERSION}"

    androidTestCompile "com.google.dagger:dagger:${DAGGER_VERSION}"
    androidTestApt 'com.google.dagger:dagger-compiler:2.0'
}
